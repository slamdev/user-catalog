buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.4.0.RELEASE'
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'com.gorylenko.gradle-git-properties' version '1.4.17'
    id 'org.hidetake.ssh' version '2.4.2'
}

apply plugin: 'spring-boot'
apply plugin: 'com.github.slamdev.catalog.plugin.EC2-plugin'

apply from: '../gradle/deploy.gradle'

sourceCompatibility = 1.8

ext {
    deployHost = { ec2Instances.findOne(['tag': ['admin']]).publicDnsName }
}

gitProperties {
    keys = [
            'git.branch', 'git.commit.id', 'git.commit.id.abbrev', 'git.commit.user.name',
            'git.commit.user.email', 'git.commit.message.short', 'git.commit.message.full', 'git.commit.time'
    ]
}

processResources {
    filesMatching('**/application.properties') {
        expand(project.properties + project.rootProject.properties)
    }
}

repositories {
    mavenLocal()
    maven {
        url "http://dl.bintray.com/slamdev/maven"
    }
    jcenter()
    mavenCentral()
}

dependencies {
    compile 'de.codecentric:spring-boot-admin-server:1.3.6'
    compile 'de.codecentric:spring-boot-admin-server-ui:1.3.6'
    compile 'org.springframework.cloud:spring-cloud-netflix-core:1.1.3.RELEASE'
}

task createInstance << {
    ec2Instances.createInstance(['Name': 'admin', 'tag': 'admin'])
}
